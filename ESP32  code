#include <WiFi.h>
#include <PubSubClient.h>
#include <HardwareSerial.h>

#define WIFI_SSID "Tspl_25 4G"
#define WIFI_PASSWORD "Techn@ride"
#define MQTT_BROKER "192.168.1.20"  // BBB IP address
#define MQTT_PORT 1883

HardwareSerial MySerial(1);  // UART1
WiFiClient espClient;
PubSubClient client(espClient);

void setup_wifi() {
    delay(10);
    Serial.println("Connecting to WiFi...");
    WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
    while (WiFi.status() != WL_CONNECTED) {
        delay(500);
        Serial.print(".");
    }
    Serial.println("\nWiFi connected.");
}

void reconnect_mqtt() {
    while (!client.connected()) {
        Serial.print("Connecting to MQTT...");
        if (client.connect("ESP32Client")) {
            Serial.println("Connected.");
        } else {
            Serial.print("Failed, rc=");
            Serial.print(client.state());
            Serial.println(" Retrying in 5 seconds...");
            delay(5000);
        }
    }
}

void setup() {
    Serial.begin(115200);
    MySerial.begin(115200, SERIAL_8N1, 16, 17);  // RX=16, TX=17

    setup_wifi();
    client.setServer(MQTT_BROKER, MQTT_PORT);
}

void loop() {
    if (!client.connected()) {
        reconnect_mqtt();
    }
    client.loop();

    if (MySerial.available()) {
        String receivedData = MySerial.readStringUntil('\n');
        Serial.println("Received: " + receivedData);

        // Extract values from received data
        int vib_value;
        float temp_value;
        if (sscanf(receivedData.c_str(), "Vib:%d, Temp:%fC", &vib_value, &temp_value) == 2) {
            char vib_msg[10], temp_msg[10];
            sprintf(vib_msg, "%d", vib_value);
            sprintf(temp_msg, "%.2f", temp_value);

            client.publish("machine/vibration", vib_msg);
            client.publish("machine/temperature", temp_msg);
        }
    }
}
